plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.intellij:forms_rt:7.0.3'
    implementation 'net.sf.sevenzipjbinding:sevenzipjbinding:16.02-2.01'
    implementation 'net.sf.sevenzipjbinding:sevenzipjbinding-all-platforms:16.02-2.01'
    implementation 'net.lingala.zip4j:zip4j:1.3.3'
}

sourceSets {
    main {
         java {
            srcDirs = ['src']
         }
    }
}

jar {
    manifest {
        attributes("Main-Class": "info.spotcomms.trammel.Start")
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

def getVersionGit() {
    def versionGit = ""
    try {
        def gitAvailable = "command -v git".execute()
        gitAvailable.waitFor()
        if(gitAvailable.exitValue() == 0) {
            def commit = "git rev-parse --short HEAD".execute().text.trim()
            def commitDate = "git show -s --format=%cs ".concat(commit).execute().text.trim()
            versionGit = "_" + commitDate + '_' + commit
        } else {
            println("git is unavailable")
        }
    } catch (Exception e) {
        println("unable to check for git")
    }
    return versionGit
}
version = '3.0.2' + getVersionGit()
